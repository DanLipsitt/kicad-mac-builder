cmake_minimum_required( VERSION 3.6.2 )
project( KiCadMacBuilder )
include( ExternalProject )

if( NOT DEFINED KICAD_CMAKE_BUILD_TYPE )
  message( FATAL_ERROR "KICAD_CMAKE_BUILD_TYPE must be set.  Please see the README or try build.py." )
endif ()
if( NOT DEFINED KICAD_TAG AND NOT DEFINED KICAD_SOURCE_DIR)
  message( FATAL_ERROR "KICAD_TAG or KICAD_SOURCE_DIR must be set.  Please see the README or try build.py." )
endif ()
if( NOT DEFINED PACKAGES3D_TAG )
  message( FATAL_ERROR "PACKAGES3D_TAG must be set.  Please see the README or try build.py." )
endif ()
if( NOT DEFINED FOOTPRINTS_TAG )
  message( FATAL_ERROR "FOOTPRINTS_TAG must be set.  Please see the README or try build.py." )
endif ()
if( NOT DEFINED TRANSLATIONS_TAG )
  message( FATAL_ERROR "TRANSLATIONS_TAG must be set.  Please see the README or try build.py." )
endif ()
if( NOT DEFINED SYMBOLS_TAG )
  message( FATAL_ERROR "SYMBOLS_TAG must be set.  Please see the README or try build.py." )
endif ()
if( NOT DEFINED DOCS_TARBALL_URL)
  message( FATAL_ERROR "DOCS_TARBALL_URL must be set.  Please see the README or try build.py." )
endif ()
if( NOT DEFINED TEMPLATES_TAG)
  message( FATAL_ERROR "TEMPLATES_TAG must be set.  Please see the README or try build.py." )
endif ()

if( NOT DEFINED MACOS_MIN_VERSION )
message( FATAL_ERROR "MACOS_MIN_VERSION must be set.  For example 'cmake -DMACOS_MIN_VERSION=10.13 ../kicad-mac-builder'" )
endif ()

set( CMAKE_VERBOSE_MAKEFILE ON )
set( BIN_DIR ${CMAKE_SOURCE_DIR}/bin )

set( TRANSLATIONS_URL https://gitlab.com/kicad/code/kicad-i18n.git )
set( SYMBOLS_URL https://gitlab.com/kicad/libraries/kicad-symbols.git )
set( PACKAGES3D_URL https://gitlab.com/kicad/libraries/kicad-packages3D.git )
set( TEMPLATES_URL https://gitlab.com/kicad/libraries/kicad-templates.git )
set( FOOTPRINTS_URL https://gitlab.com/kicad/libraries/kicad-footprints.git )

set( PYTHON_VERSION 2.7.16 )
set( PYTHON_URL https://www.python.org/ftp/python/${PYTHON_VERSION}/Python-${PYTHON_VERSION}.tgz )
set( PYTHON_SHA1 e824c633a59fa2ca1dc2123de855007e64f9de98 )
set( PYTHON_INSTALL_DIR ${CMAKE_BINARY_DIR}/python-dest/Library/Frameworks ) # Ending in /Library/Frameworks is important here

set( WXPYTHON_VERSION 4.1.1 )
set( WXPYTHON_URL https://files.pythonhosted.org/packages/source/w/wxPython/wxPython-${WXPYTHON_VERSION}.tar.gz )
set( WXPYTHON_SHA1 f952f6224a7c7f000df2150ddfc27edfb4b8f31c ) # This is for wxpython, not wxwidgets.
set( wxwidgets_INSTALL_DIR ${CMAKE_BINARY_DIR}/wxwidgets-dest )
set( wxpython_INSTALL_DIR ${CMAKE_BINARY_DIR}/wxpython-dest )
set( six_DIR ${CMAKE_BINARY_DIR}/six/src/six )

set( ngspice_INSTALL_DIR ${CMAKE_BINARY_DIR}/ngspice-dest )

set( KICAD_INSTALL_DIR ${CMAKE_BINARY_DIR}/kicad-dest )

set( KICAD_CMAKE_ARGS -DDEFAULT_INSTALL_PATH=/Library/Application\ Support/kicad )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DCMAKE_C_COMPILER=/usr/bin/clang -DCMAKE_CXX_COMPILER=/usr/bin/clang++ )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DCMAKE_OSX_DEPLOYMENT_TARGET=${MACOS_MIN_VERSION} )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DwxWidgets_CONFIG_EXECUTABLE=${wxwidgets_INSTALL_DIR}/bin/wx-config )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DKICAD_SCRIPTING=OFF -DKICAD_SCRIPTING_MODULES=OFF -DKICAD_SCRIPTING_WXPYTHON=OFF )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DKICAD_SCRIPTING_ACTION_MENU=OFF -DKICAD_SCRIPTING_PYTHON3=OFF -DKICAD_SCRIPTING_WXPYTHON_PHOENX=OFF )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DPYTHON_EXECUTABLE=${PYTHON_INSTALL_DIR}/Python.framework/Versions/2.7/bin/python2.7 )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DPYTHON_INCLUDE_DIR=${PYTHON_INSTALL_DIR}/Python.framework/Versions/2.7/include/python2.7/ )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DPYTHON_LIBRARY=${PYTHON_INSTALL_DIR}/Python.framework/Versions/2.7/lib/libpython2.7.dylib )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DPYTHON_SITE_PACKAGE_PATH=${wxwidgets_INSTALL_DIR}/lib/python2.7/site-packages )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DPYTHON_FRAMEWORK=${PYTHON_INSTALL_DIR}/Python.framework )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DCMAKE_INSTALL_PREFIX=${KICAD_INSTALL_DIR} )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DKICAD_USE_OCE=OFF -DKICAD_USE_OCC=ON )

# FindOpenCASCADE.cmake finds /usr/local/lib, but not the Homebrew one.  fixup_bundle doesn't work with /usr/local/lib as a dirs argument, so
# if OCC is installed by brew, we're going to pass in the library and include dir manually. If not, we aren't passing in anything
# which means that FindOpenCASCADE.cmake will do its regular job.
# This should be fixed by figuring out why fixup_bundle isn't working with /usr/local/lib in the dirs argument.
execute_process(COMMAND brew --prefix opencascade OUTPUT_VARIABLE BREW_OCC_BASEDIR RESULT_VARIABLE FOUND_BREW_OCC_BASEDIR_EXIT_CODE OUTPUT_STRIP_TRAILING_WHITESPACE)
if ( ${FOUND_BREW_OCC_BASEDIR_EXIT_CODE} EQUAL 0 )
    message( "OpenCascade appears to have been installed by brew." )
    if ( ${CMAKE_OSX_ARCHITECTURES} MATCHES "arm64" )
      get_filename_component(BREW_LIBTKERNEL_REALPATH /opt/homebrew/lib/libTKernel.dylib REALPATH)
      get_filename_component(BREW_OCC_LIBRARY_DIR ${BREW_LIBTKERNEL_REALPATH} DIRECTORY)
      get_filename_component(BREW_OCC_INCLUDE_DIR /opt/homebrew/include/opencascade REALPATH)
      set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DOCC_INCLUDE_DIR=${BREW_OCC_INCLUDE_DIR} -DOCC_LIBRARY_DIR=${BREW_OCC_LIBRARY_DIR} )
    else()
      get_filename_component(BREW_LIBTKERNEL_REALPATH /usr/local/lib/libTKernel.dylib REALPATH)
      get_filename_component(BREW_OCC_LIBRARY_DIR ${BREW_LIBTKERNEL_REALPATH} DIRECTORY)
      get_filename_component(BREW_OCC_INCLUDE_DIR /usr/local/include/opencascade REALPATH)
      set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DOCC_INCLUDE_DIR=${BREW_OCC_INCLUDE_DIR} -DOCC_LIBRARY_DIR=${BREW_OCC_LIBRARY_DIR} )
    endif()
endif()

set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DKICAD_SPICE=ON )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DNGSPICE_INCLUDE_DIR=${ngspice_INSTALL_DIR}/include )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DNGSPICE_LIBRARY=${ngspice_INSTALL_DIR}/lib/libngspice.dylib )
set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DCMAKE_BUILD_TYPE=${KICAD_CMAKE_BUILD_TYPE} )

if(${KICAD_VERSION_EXTRA})
  set( KICAD_CMAKE_ARGS ${KICAD_CMAKE_ARGS} -DKICAD_VERSION_EXTRA=${KICAD_VERSION_EXTRA} )
endif()

set( DMG_DIR ${CMAKE_BINARY_DIR}/dmg )


message( "CMake Setting Summary for kicad-mac-builder" )
message( "KICAD_TAG: ${KICAD_TAG}" )
message( "KICAD_SOURCE_DIR: ${KICAD_SOURCE_DIR}" )
message( "PACKAGES3D_TAG: ${PACKAGES3D_TAG}" )
message( "SYMBOLS_TAG: ${SYMBOLS_TAG}" )
message( "FOOTPRINTS_TAG: ${FOOTPRINTS_TAG}" )
message( "TRANSLATIONS_TAG: ${TRANSLATIONS_TAG}" )
message( "TEMPLATES_TAG: ${TEMPLATES_TAG}" )
message( "DOCS_TARBALL_URL: ${DOCS_TARBALL_URL}" )
message( "KICAD_CMAKE_ARGS: ${KICAD_CMAKE_ARGS}" )
message( "SYMBOLS_URL: ${SYMBOLS_URL}" )
message( "FOOTPRINTS_URL: ${FOOTPRINTS_URL}" )
message( "PACKAGES3D_URL: ${PACKAGES3D_URL}" )
message( "TEMPLATES_URL: ${TEMPLATES_URL}" )
message( "TRANSLATIONS_URL: ${TRANSLATIONS_URL}" )

include( python.cmake )
include( wxpython.cmake )
include( ngspice.cmake)
include( docs.cmake )
include( kicad.cmake )
include( translations.cmake )

include( footprints.cmake )
include( symbols.cmake )
include( templates.cmake )
include( packages3d.cmake )

include( package_kicad_nightly.cmake )
include( package_kicad_unified.cmake )
include( package_extras.cmake )
